/*
  Version 1.7

  Procedure Name:
  AEdomeAFL_WxH_LegacyTemplate

  Description:
  Creates the attribute editor controls for the domeAFL_WxH shader

  Version History
  -----------------
  
  Version 1.7 - 2015-03-07 

  Version 1.5 - July 7, 2014
  Created a Fallback module to handle the updated Node IDs

  Updated Help URL

  Version 1.4 - Oct 6, 2013 
  Initial Release 

  Copied base AE template file from domeAFL_FOV_Stereo node

  Added the dome controls

  Author:
  Andrew Hazelden

  Contact:
  andrew@andrewhazelden.com

*/

//---------------------------------------------------------------------------------
// New Dome Radius GUI Controls
//---------------------------------------------------------------------------------

//Return the name of the Node's Attr
global proc string dome_AEStripNodeName (string $nodeName)
{
	string $buffer[];
	tokenize $nodeName "." $buffer;
	return $buffer[1];
}


//Return the base name of the Node
global proc string dome_AEStripAttrName (string $nodeName)
{
	string $buffer[];
	tokenize $nodeName "." $buffer;
	return $buffer[0];
}


//Find the shape node
//example: print `getObjectShapeNode( "domeAFL_WxH_Camera1")`;
global proc string getObjectShapeNode ( string $object ) {
  string $shapes[] = `listRelatives -children -shapes $object`;
  return $shapes[0];
}


//Find the object's parent node
//example: print `getObjectParentNode( "domeAFL_WxH_CameraShape2")`;
global proc string getObjectParentNode ( string $object ) {
  string $shapes[] = `listRelatives -parent -shapes $object`;
  return $shapes[0];
}


//This creates the controls in the attribute editor
global proc AEdomeAFL_WxH_AddDynamicAttrs( string $nodeName )
{
  //separate the node name from the node.attribute name
  string $baseNodeName = dome_AEStripAttrName($nodeName);
  
  int $NewDomeSetup = 0;
  
  //Add a version number attribute to the extra attribute fields
  string $attrName ="domeVersion";
  int $domeVersion = 1;
  if (!`attributeExists $attrName $baseNodeName`) {
    addAttr -ln $attrName -at long -min 0 $baseNodeName;
    setAttr ($baseNodeName+"."+$attrName) $domeVersion;
    print("Adding custom attribute " + $baseNodeName +"." +$attrName + "\n");
    
    $NewDomeSetup = 1;
    
    //Add the Attribute Editor node "help menu" link
    addAttributeEditorNodeHelp(
      $baseNodeName,
      "showHelp -absolute \"https://github.com/zicher3d-org/domemaster-stereo-shader/wiki/_pages\"");
  }
}


//Create the attribute editor view for the domeAFL_WxH_Legacy node
global proc AEdomeAFL_WxH_LegacyTemplate( string $nodeName )
{
	AEswatchDisplay $nodeName;
	editorTemplate -beginScrollLayout;

    editorTemplate -beginLayout "Domemaster WxH Legacy Shader" -collapse 0;
      editorTemplate 	-beginNoOptimize;
      
      //Add the custom dynamic attributes to the node
      editorTemplate -callCustom "AEdomeAFL_WxH_AddDynamicAttrs" "AEdomeAFL_WxH_AddDynamicAttrs" "Diameter";
      
      editorTemplate -label "Diameter" -annotation "\"Diameter\" controls the final rendered view. The default is 1.0" -addControl "Diameter";
      
      editorTemplate -label "Height" -annotation  "This attribute controls the height of the dome shape. The default is 0.5" -addDynamicControl "Height";
      
      editorTemplate -label "View Offset" -annotation "The \"View Offset\" controls shifts the current ray angles on the X/Y/Z axis. The valid \"View Offset\" ranges are -1.0 to 1.0" -addControl "View_Offset";

      editorTemplate -endNoOptimize;
    editorTemplate -endLayout;
       
    //Define the image orientation attributes
    editorTemplate -beginLayout "Image Orientation" -collapse 0;
      editorTemplate 	-beginNoOptimize;
      
      editorTemplate -label "Flip X" -annotation "Flips the image horizontally" -addControl "Flip_Ray_X";	
      
      editorTemplate -label "Flip Y" -annotation  "Flips the image vertically." -addControl "Flip_Ray_Y";
      
      editorTemplate -endNoOptimize;
    editorTemplate -endLayout;

    AEdependNodeTemplate $nodeName;
    
    //Add any new controls that aren't listed above to the bottom of the attribute editor window 
    editorTemplate -addExtraControls;
    
    //Hide the raw numerical camera and view offset fields
    editorTemplate -suppress "caching";
    editorTemplate -suppress "nodeState";

  editorTemplate -endScrollLayout;
}
