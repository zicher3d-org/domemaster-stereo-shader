// Copyright (C) 1997-2006 Autodesk, Inc., and/or its licensors.
// All rights reserved.
//
// The coded instructions, statements, computer programs, and/or related
// material (collectively the "Data") in these files contain unpublished
// information proprietary to Autodesk, Inc. ("Autodesk") and/or its licensors,
// which is protected by U.S. and Canadian federal copyright law and by
// international treaties.
//
// The Data is provided for use exclusively by You. You have the right to use,
// modify, and incorporate this Data into other products for purposes authorized 
// by the Autodesk software license agreement, without fee.
// THE SOFTWARE IS PROVIDED "AS IS", WITHOUT WARRANTY OF ANY KIND. AUTODESK
// DOES NOT MAKE AND HEREBY DISCLAIMS ANY EXPRESS OR IMPLIED WARRANTIES
// INCLUDING, BUT NOT LIMITED TO, THE WARRANTIES OF NON-INFRINGEMENT,
// MERCHANTABILITY OR FITNESS FOR A PARTICULAR PURPOSE, OR ARISING FROM A COURSE 
// OF DEALING, USAGE, OR TRADE PRACTICE. IN NO EVENT WILL AUTODESK AND/OR ITS
// LICENSORS BE LIABLE FOR ANY LOST REVENUES, DATA, OR PROFITS, OR SPECIAL,
// DIRECT, INDIRECT, OR CONSEQUENTIAL DAMAGES, EVEN IF AUTODESK AND/OR ITS
// LICENSORS HAS BEEN ADVISED OF THE POSSIBILITY OR PROBABILITY OF SUCH DAMAGES.

//
//  Autodesk Script File
//
//  Procedure Name: AEmia_physicalskyTemplate
//
//  Description Name;
//	Creates the attribute editor controls for the mia_physicalsky Nodes
//
//  Input Value:
//	nodeName 
//
//  Output Value:
//	None
//

global proc miUpdateSkyCameras(string $skyShader)
{
	string $exposureControl;
	string $connections[] = `listConnections ($skyShader + ".miSkyExposure")`;
	if(size($connections))
		$exposureControl = $connections[0];
		
	// hookup sky as environment shader to all render cameras
	string $cameras[] = `ls -type camera`;
	for ($camera in $cameras) 
	{
		string $cmd;
		if (!`getAttr ($camera + ".orthographic")`) 
		{
			$cmd = ("connectAttr -force " + $skyShader + ".message " + $camera + ".miEnvironmentShader");
			catchQuiet(`eval $cmd`);
			
			if(size($exposureControl))
			{
				$cmd = ("connectAttr -force " + $exposureControl + ".message " + $camera + ".miLensShader");
				catchQuiet(`eval $cmd`);
			}
		}
	}
}

global proc miRemoveSkyCameras(string $skyShader)
{
	string $result = `confirmDialog -m (uiRes("m_AEmia_physicalskyTemplate.kRemoveCamConnect")) 
						-button (uiRes("m_AEmia_physicalskyTemplate.kYes"))
						-button (uiRes("m_AEmia_physicalskyTemplate.kNo")) 
						-defaultButton (uiRes( "m_AEmia_physicalskyTemplate.kNo"))`;
	if ($result == (uiRes( "m_AEmia_physicalskyTemplate.kNo"))) {
		return;
	} 
  
	string $exposureControl;
	string $connections[] = `listConnections ($skyShader + ".miSkyExposure")`;
	if(size($connections))
		$exposureControl = $connections[0];
		
	string $cameras[] = `listConnections -type camera ($skyShader + ".message")`;
	for ($camera in $cameras) 
	{
		disconnectAttr 
			($skyShader + ".message")
			($camera + ".miEnvironmentShader");
	}
	
	if(size($exposureControl))
	{
		string $exposureCameras[] = `listConnections -type camera ($exposureControl + ".message")`;
		for ($camera in $exposureCameras) 
		{
			disconnectAttr
				($exposureControl + ".message")
				($camera + ".miLensShader");
		}
	}
}

global proc miSkyCamerasButtonCreate(string $attr)
{
	string $buffer[];
	tokenize($attr, ".", $buffer);
	string $node = (size($buffer) > 0) ? $buffer[0] : "";

	button -label (uiRes("m_AEmia_physicalskyTemplate.kUpdateCameraConnections")) 
		-command ("miUpdateSkyCameras " + $node)
		updateCamerasButton;

	button -label (uiRes("m_AEmia_physicalskyTemplate.kRemoveCameraConnections")) 
		-command ("miRemoveSkyCameras " + $node)
		removeCamerasButton;
}

global proc miSkyCamerasButtonReplace(string $attr)
{
	string $buffer[];
	tokenize($attr, ".", $buffer);
	string $node = (size($buffer) > 0) ? $buffer[0] : "";

	button	-edit
		-command ("miUpdateSkyCameras " + $node)
		updateCamerasButton;

	button -edit 
		-command ("miRemoveSkyCameras " + $node)
		removeCamerasButton;
}

global proc AEmia_skyUnitConversionReplace(string $attr)
{
	attrFieldSliderGrp -edit
		-attribute ($attr + "R")
		-minValue 0.0
		-maxValue 1.0
		-fieldMaxValue 1000.0
		redUnitConversionControl;
	attrFieldSliderGrp -edit
		-attribute ($attr + "G")
		-minValue 0.0
		-maxValue 1.0
		-fieldMaxValue 1000.0
		greenUnitConversionControl;
	attrFieldSliderGrp -edit
		-attribute ($attr + "B")
		-minValue 0.0
		-maxValue 1.0
		-fieldMaxValue 1000.0
		blueUnitConversionControl;
}

global proc AEmia_skyUnitConversionCreate(string $attr)
{
	attrFieldSliderGrp	-label (uiRes("m_AEmia_physicalskyTemplate.kRedUnitConversionControl"))
						-precision 6
						-hideMapButton true
						redUnitConversionControl;
	attrFieldSliderGrp	-label (uiRes("m_AEmia_physicalskyTemplate.kGreenUnitConversionControl"))
						-precision 6
						-hideMapButton true
						greenUnitConversionControl;
	attrFieldSliderGrp	-label (uiRes("m_AEmia_physicalskyTemplate.kBlueUnitConversionControl"))
						-precision 6
						-hideMapButton true
						blueUnitConversionControl;

	AEmia_skyUnitConversionReplace $attr;
}

global proc AEmia_physicalskyTemplate ( string $nodeName )
{
	AEswatchDisplay $nodeName;
	editorTemplate -beginScrollLayout;

	//Shading
	//
	editorTemplate -beginLayout (uiRes("m_AEmia_physicalskyTemplate.kShading"))
		-collapse 0;

		editorTemplate	-addControl "on" "AEmia_physicalskyEnable";
		editorTemplate	-addControl "multiplier";

		editorTemplate	-callCustom
							"AEmia_skyUnitConversionCreate"
							"AEmia_skyUnitConversionReplace"
							"rgb_unit_conversion";

		editorTemplate	-addControl "haze";
		editorTemplate	-label (uiRes("m_AEmia_physicalskyTemplate.kRBShift"))
						-addControl "redblueshift";
		editorTemplate	-addControl "saturation";
		
		editorTemplate	-label (uiRes("m_AEmia_physicalskyTemplate.kHorizonHeight"))
						-addControl "horizon_height";
		editorTemplate	-label (uiRes("m_AEmia_physicalskyTemplate.kHorizonBlur"))
						-addControl "horizon_blur";
		editorTemplate	-label (uiRes("m_AEmia_physicalskyTemplate.kGroundColor"))
						-addControl "ground_color";
		editorTemplate	-label (uiRes("m_AEmia_physicalskyTemplate.kNightColor"))
						-addControl "night_color";
						
		editorTemplate	-label (uiRes("m_AEmia_physicalskyTemplate.kSunDirection"))
						-addControl "sun_direction";
		editorTemplate	-addControl "sun";
		
		editorTemplate	-label (uiRes("m_AEmia_physicalskyTemplate.kSunDiskIntensity"))
						-addControl "sun_disk_intensity";
		editorTemplate	-label (uiRes("m_AEmia_physicalskyTemplate.kSunDiskScale"))
						-addControl "sun_disk_scale";
		editorTemplate	-label (uiRes("m_AEmia_physicalskyTemplate.kSunGlowIntensity"))
						-addControl "sun_glow_intensity";
		
		editorTemplate	-label (uiRes("m_AEmia_physicalskyTemplate.kUseBackground"))
						-addControl "use_background" "AEmia_physicalskyBackgroundEnable";
		editorTemplate	-addControl "background";

		editorTemplate	-label (uiRes("m_AEmia_physicalskyTemplate.kYisUp"))
						-addControl "y_is_up";
		editorTemplate	-addControl "flags";
		editorTemplate	-label (uiRes("m_AEmia_physicalskyTemplate.kVisibilityDistance")) -addControl "visibility_distance";

	separator -style "none" -width 490 -height 10;

	rowLayout
		-numberOfColumns 2
		-columnWidth 2 180;

		text -label "";
		editorTemplate -callCustom
			"miSkyCamerasButtonCreate"
			"miSkyCamerasButtonReplace"
			"message";
		text -label "";

	setParent ..;

	separator -style "none" -width 490 -height 10;
	editorTemplate -endLayout;

	// include/call base class/node attributes
	//
	AEmentalrayBaseTemplate($nodeName);

	editorTemplate -endScrollLayout;
}



//dims all controls whens "On" is not activated
global proc AEmia_physicalskyEnable(string $node)
{
	int $pluginLoaded = `pluginInfo -q -loaded Mayatomr`;

	editorTemplate -dc $node "on" (! $pluginLoaded);

	int $dim = ($pluginLoaded && `getAttr ($node + ".on")`) ? 0 : 1;

	editorTemplate -dimControl $node "multiplier" $dim;
	editorTemplate -dimControl $node "rgb_unit_conversion" $dim;
	editorTemplate -dimControl $node "haze" $dim;
	editorTemplate -dimControl $node "redblueshift" $dim;
	editorTemplate -dimControl $node "saturation" $dim;
	editorTemplate -dimControl $node "horizon_height" $dim;
	editorTemplate -dimControl $node "horizon_blur" $dim;
	editorTemplate -dimControl $node "ground_color" $dim;
	editorTemplate -dimControl $node "night_color" $dim;
	editorTemplate -dimControl $node "sun_direction" $dim;
	editorTemplate -dimControl $node "sun" $dim;
	editorTemplate -dimControl $node "sun_disk_intensity" $dim;
	editorTemplate -dimControl $node "sun_disk_scale" $dim;
	editorTemplate -dimControl $node "sun_glow_intensity" $dim;
	editorTemplate -dimControl $node "use_background" $dim;
	editorTemplate -dimControl $node "background" $dim;
	editorTemplate -dimControl $node "y_is_up" $dim;
	editorTemplate -dimControl $node "flags" $dim;
}


//dims "Background" control whens "UseBackground" is not activated
global proc AEmia_physicalskyBackgroundEnable(string $node)
{
	int $pluginLoaded = `pluginInfo -q -loaded Mayatomr`;

	editorTemplate -dc $node "use_background" (! $pluginLoaded);

	int $dim = ($pluginLoaded && `getAttr ($node+".use_background")`) ? 0 : 1;

	editorTemplate -dimControl $node "background" $dim;
}
